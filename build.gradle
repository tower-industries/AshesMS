buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.flywaydb:flyway-mysql:10.17.0"
	}
}

plugins {
	id 'application'
	id 'java'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'org.flywaydb.flyway' version '10.17.0'
}

repositories {
	mavenCentral()
	gradlePluginPortal()

	maven {
		url "https://download.red-gate.com/maven/release"
	}
}

dependencies {
	implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.17'
    implementation 'net.jcip:jcip-annotations:1.0'

    // Database
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'com.mysql:mysql-connector-j:8.4.0'
    implementation 'org.jdbi:jdbi3-core:3.45.1'

    // Networking
    implementation 'io.netty:netty-all:4.1.109.Final'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'

    // Scripting
    implementation 'org.graalvm.js:js:23.0.4'
    implementation 'org.graalvm.js:js-scriptengine:24.0.1'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
}

application {
	mainClass = 'net.server.Server'
}

jar {
	manifest {
		attributes "Main-Class": "net.server.Server"
	}
}

flyway {
	locations = ['filesystem:database/migration']
	url = System.getenv('DB_URL')
	user = System.getenv('DB_USER')
	password = System.getenv('DB_PASS')
	cleanDisabled = false
}

build {
	dependsOn shadowJar 
}

test {
	useJUnitPlatform()
}

task runJar(type: Exec) {
	dependsOn build
	commandLine 'java', '-Xmx2048m', '-Dwz-path=wz', '-jar', 'build/libs/AshesMS-all.jar'
}
